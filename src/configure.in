AC_INIT(system-config-printer, 1.1.16)
AC_CONFIG_SRCDIR(system-config-printer.py)
AM_INIT_AUTOMAKE([dist-xz dist-bzip2 subdir-objects 1.6])
IT_PROG_INTLTOOL
AM_GNU_GETTEXT([external])
AM_PATH_PYTHON
PACKAGE="AC_PACKAGE_NAME"
VERSION="AC_PACKAGE_VERSION"
GETTEXT_PACKAGE="AC_PACKAGE_NAME"
CATOBJEXT=".gmo"
DATADIRNAME=share
AC_SUBST(PACKAGE)
AC_SUBST(VERSION)
AC_SUBST(GETTEXT_PACKAGE)
AC_SUBST(CATOBJEXT)
AC_SUBST(DATADIRNAME)

# Let distributor specify if they want to use a vendor with desktop-file-install
AC_ARG_WITH(desktop-vendor,
              [AC_HELP_STRING([--with-desktop-vendor],
                              [Specify the vendor for use in calls to desktop-file-install @<:@default=@:>@])],,
                              [with_desktop_vendor=""])

VENDOR=$with_desktop_vendor
if test "x$VENDOR" = "x"; then
    DESKTOPVENDOR=
    DESKTOPPREFIX=
else
    DESKTOPVENDOR="--vendor $VENDOR"
    DESKTOPPREFIX="$VENDOR-"
fi
AC_SUBST(DESKTOPVENDOR)
AC_SUBST(DESKTOPPREFIX)

AC_ARG_WITH(udev-rules,
	    [AC_HELP_STRING([--with-udev-rules],
			    [Enable automatic USB print queue configuration @<:@default=no@:>@])],
	    [],
	    [with_udev_rules=no])
AM_CONDITIONAL([UDEV_RULES], [test x$with_udev_rules != xno])

if test x$with_udev_rules != xno; then
   AM_PROG_CC_C_O
fi

AC_ARG_WITH(polkit-1,
	    [AC_HELP_STRING([--with-polkit-1],
			    [Enable support for polkit-1 @<:@default=no@:>@])],
			    [],
			    [with_polkit_1=no])
WITH_POLKIT_1=False
if test x$with_polkit_1 != xno; then
    WITH_POLKIT_1=True
fi
AC_SUBST(WITH_POLKIT_1)

ALL_LINGUAS="ar as bg bn_IN bn bs ca cs cy da de el en_GB es et fa fi fr gu he hi hr hu hy id is it ja ka kn ko lo lv mai mk ml mr ms my nb nl nn or pa pl pt_BR pt ro ru si sk sl sr@latin sr sv ta te th tr uk vi zh_CN zh_TW"
AC_CONFIG_FILES([
Makefile
po/Makefile.in
my-default-printer
system-config-printer
system-config-printer-applet
])
AC_OUTPUT
